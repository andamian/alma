import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'war'
}

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        url = 'https://www.cadc-ccda.hia-iha.nrc-cnrc.gc.ca/m2repo'
    }
}

group = 'org.opencadc'
sourceCompatibility = 1.8
version = '1.1.16'

ext {
    default_app_name = 'obscore'
}

war {
    if (project.hasProperty('alma_tap_app_name')) {
        eachFile {
            if (it.name.equals('capabilities.xml') || it.name.equals('service.json')) {
                filter(ReplaceTokens, tokens: [app_name: project.alma_tap_app_name])
            }
        }
        archiveName = project.alma_tap_app_name + '##' + project.version + '.war'
    } else {
        eachFile {
            if (it.name.equals('capabilities.xml') || it.name.equals('service.json')) {
                filter(ReplaceTokens, tokens: [app_name: project.default_app_name])
            }
        }
        archiveName = project.default_app_name + '##' + project.version + '.war'
    }

    // Include the swagger-ui so that /tap provides the TAP API documentation
    from('../') {
        include 'swagger-ui/'
    }
}

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestRuntime.extendsFrom testRuntime
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-core:[2.13.0,3.0)'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:[2.13.0,3.0)'
    implementation 'com.oracle.ojdbc:ojdbc8:19.3.0.0'
    implementation 'org.restlet.jee:org.restlet.ext.servlet:2.0.3'
    implementation 'org.opencadc:cadc-tap-server-oracle:[1.2.5,)'
    implementation 'org.opencadc:cadc-tap-server:[1.1.5,)'
    implementation 'org.opencadc:cadc-adql:[1.1,)'
    implementation 'org.opencadc:cadc-vosi:[1.3,2.0)'

    runtimeOnly 'org.opencadc:cadc-registry:[1.5.9,2.0)'

    testImplementation 'junit:junit:[4.12,5.0)'
    testImplementation 'org.opencadc:cadc-test-uws:[1.1.1,)'
    testImplementation 'org.opencadc:cadc-test-vosi:[1.0.2,)'
    testImplementation 'org.opencadc:cadc-test-tap:[1.1,)'

    intTestImplementation 'org.opencadc:cadc-test-uws:[1.1.1,)'
    intTestImplementation 'org.opencadc:cadc-test-vosi:[1.0.2,)'
    intTestImplementation 'org.opencadc:cadc-test-tap:[1.1,)'
}

sourceSets {
    intTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/intTest/java')
        }

        // set the intTest resource directory
        resources.srcDir file('src/intTest/resources')
    }
}

tasks.withType(Test) {
    // Reset the report destinations so that intTests go to their own page
    reports.html.destination = file(reporting.baseDir.getAbsolutePath() + '/' + name)

    // Assign all Java system properties from the command line to the tests
    systemProperties System.properties as Map<String, ?>
}

task intTest(type: Test) {
    // set the configuration context
    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath

    // run the tests always
    outputs.upToDateWhen { false }
}
