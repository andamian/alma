import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'war'
}

repositories {
    mavenCentral()
    mavenLocal()
}

sourceCompatibility = 11
version = "1.2.5"

war {
    eachFile {
        if (it.name.equals('capabilities.xml') || it.name.equals('service.json')) {
            filter(ReplaceTokens, tokens: [app_name: 'soda', version: version])
        }
    }
    archiveName = 'soda.war'

    manifest {
        attributes "Implementation-Version": version
        attributes "Implementation-Title": "SODA"
    }

    // Include the swagger-ui so that /soda provides the soda API documentation
    from('../') {
        include 'swagger-ui/'
    }
}

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestRuntime.extendsFrom testRuntime
}

dependencies {
    implementation 'org.opencadc:alma-lib:[1.3.5,2.0)'
    implementation 'org.opencadc:cadc-dali:[1.2.10,2.0)'
    implementation 'org.opencadc:cadc-log:[1.1.5,2.0)'
    implementation 'org.opencadc:cadc-soda-server:[1.2.1,1.3.0)'
    implementation 'org.opencadc:cadc-vosi:[1.4.3,1.5.0)'

    testImplementation 'junit:junit:[4.12,5.0)'
    testImplementation 'org.mockito:mockito-core:[3.9,4.0)'
    testImplementation 'org.opencadc:cadc-test-uws:[1.2.2,)'
    testImplementation 'org.opencadc:cadc-test-vosi:[1.0.2,)'
}

sourceSets {
    intTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/intTest/java')
        }
    }
}

tasks.withType(Test) {
    // Reset the report destinations so that intTests go to their own page
    reports.html.destination = file(reporting.baseDir.getAbsolutePath() + '/' + name)

    // Assign all Java system properties from the command line to the tests
    systemProperties System.properties as Map<String, ?>
}

task intTest(type: Test) {
    // set the configuration context
    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath

    // run the tests always
    outputs.upToDateWhen { false }
}
