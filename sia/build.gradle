import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'war'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = 'https://www.cadc-ccda.hia-iha.nrc-cnrc.gc.ca/m2repo'
    }
}

sourceCompatibility = 11
version = '1.0.5'

// Added AFTER the version is declared.
apply from: '../properties.gradle'

war {
    eachFile {
        if (it.name.equals('capabilities.xml') || it.name.equals('service.json')) {
            filter(ReplaceTokens, tokens: [app_name: 'sia2', version: version])
        }
    }
    archiveName = 'sia2.war'

    manifest {
        attributes "Implementation-Version": version
        attributes "Implementation-Title": "Simple Image Access Version 2.0"
    }

    // Include the swagger-ui so that /sia2 provides the Sia API documentation
    from('../') {
        include 'swagger-ui/'
    }
}

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestRuntime.extendsFrom testRuntime
}

dependencies {
    implementation 'org.opencadc:cadc-access-control-identity:[1.0.8,2.0)'
    implementation 'org.opencadc:cadc-cdp:[1.2.3,2.0)'
    implementation 'org.opencadc:cadc-log:[1.1.5,1.2.0)'
    implementation 'org.opencadc:cadc-sia:[1.1.9,2.0)'
    implementation 'org.opencadc:cadc-tap:[1.0,2.0)'
    implementation 'org.opencadc:cadc-util:[1.6.0,2.0)'
    implementation 'org.opencadc:cadc-uws-server:[1.2.5,2.0)'
//    implementation 'ca.nrc.cadc:uws-util:[1.0,2.0)'

    runtimeOnly 'org.opencadc:cadc-vosi:[1.3.4,2.0)'
    runtimeOnly 'org.opencadc:cadc-registry:[1.5.9,2.0)'

    testImplementation 'junit:junit:[4.12,5.0)]'
    testImplementation 'org.opencadc:cadc-test-vosi:[1.0.2,)'
    testImplementation 'org.opencadc:cadc-test-uws:[1.2.2,)'
}

sourceSets {
    intTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/intTest/java')
        }
    }
}

tasks.withType(Test) {
    // reset the report destinations so that intTests go to their
    // own page
    reports.html.destination = file(reporting.baseDir.getAbsolutePath() + '/' + name)

    // Assign all Java system properties from
    // the command line to the tests
    systemProperties System.properties as Map<String, ?>
}

task intTest(type: Test) {
    // set the configuration context
    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath

    // run the tests always
    outputs.upToDateWhen { false }
}
