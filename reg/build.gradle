import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'war'
}

repositories {
    mavenCentral()
    mavenLocal()
}

apply from: '../properties.gradle'

war {
    eachFile {
        if (it.name.equals('capabilities.xml') || it.name.equals('service.json')) {
            filter(ReplaceTokens, tokens: [app_name: 'reg', version: version])
        }
    }
    archiveName = 'reg.war'

    // Include the swagger-ui so that /reg provides the Reg API documentation
    from('../') {
        include 'swagger-ui/'
    }
}

sourceSets {
    intTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/intTest/java')
        }
    }
}

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestRuntime.extendsFrom testRuntime
}

dependencies {
    implementation 'org.opencadc:cadc-dali:[1.2.1,2.0)'
    implementation 'org.opencadc:cadc-log:[1.1.5,1.2.0)'
    implementation 'org.opencadc:cadc-registry:[1.5.9,2.0)'
    implementation 'org.opencadc:cadc-registry-server:[1.0.9,2.0)'
    implementation 'org.opencadc:cadc-util:[1.6.0,2.0)'
    implementation 'org.opencadc:cadc-uws-server:[1.2.5,2.0)'
    implementation 'org.opencadc:cadc-vosi:[1.3.4,2.0)'

    testImplementation 'junit:junit:[4.0,5.0)'

    intTestImplementation 'org.opencadc:cadc-test-vosi:[1.0.2,)'
}


tasks.withType(Test) {
    // reset the report destinations so that intTests go to their
    // own page
    reports.html.destination = file(reporting.baseDir.getAbsolutePath() + '/' + name)

    // Assign all Java system properties from
    // the command line to the tests
    systemProperties System.properties
}

task intTest(type: Test) {
    // set the configuration context
    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath

    // run the tests always
    outputs.upToDateWhen { false }
}

test {
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        // "STARTED",
    }
}

intTest {
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        // "STARTED",
    }
}
