import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'war'

    // IntelliJ IDEA plugin here to allow integration tests to be properly loaded.
    id 'idea'
}

repositories {
    mavenCentral()
    mavenLocal()
}

apply from: '../properties.gradle'

war {
    eachFile {
        if (it.name.equals('capabilities.xml') || it.name.equals('service.json')) {
            filter(ReplaceTokens, tokens: [app_name: 'reg', version: version])
        }
    }
    archiveFileName = 'reg.war'

    manifest {
        attributes "Implementation-Version": version
        attributes "Implementation-Title": "Registry"
    }

    // Include the swagger-ui so that /reg provides the Reg API documentation
    from('../') {
        include 'swagger-ui/'
    }
}

apply from: '../int_test.gradle'

dependencies {
    implementation 'org.opencadc:cadc-dali:[1.2.1,2.0)'
    implementation 'org.opencadc:cadc-log:[1.1.7,1.2.0)'
    implementation 'org.opencadc:cadc-registry:[1.5.9,2.0)'
    implementation 'org.opencadc:cadc-registry-server:[1.0.9,2.0)'
    implementation 'org.opencadc:cadc-util:[1.6.0,2.0)'
    implementation 'org.opencadc:cadc-uws-server:[1.2.5,2.0)'
    implementation 'org.opencadc:cadc-vosi:[1.3.4,2.0)'

    testImplementation 'junit:junit:[4.0,5.0)'

    intTestImplementation 'org.opencadc:cadc-test-vosi:[1.0.2,)'
}

tasks.withType(Test) {
    // reset the report destinations so that intTests go to their
    // own page
    reports.html.destination = file(reporting.baseDir.getAbsolutePath() + '/' + name)

    // Assign all Java system properties from
    // the command line to the tests
    systemProperties System.properties
}

test {
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        // "STARTED",
    }
}
