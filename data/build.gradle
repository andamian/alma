import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'war'
}

repositories {
    mavenCentral()
    mavenLocal()
}

sourceCompatibility = 1.8

ext {
    default_app_name = 'data'
}

war {
    if (project.hasProperty('alma_fits_app_name')) {
        eachFile {
            if (it.name.equals('capabilities.xml') || it.name.equals('service.yaml')) {
                filter(ReplaceTokens, tokens: [app_name: project.alma_fits_app_name])
            }
        }
        archiveName = project.alma_fits_app_name + '.war'
    } else {
        eachFile {
            if (it.name.equals('capabilities.xml') || it.name.equals('service.yaml')) {
                filter(ReplaceTokens, tokens: [app_name: project.default_app_name])
            }
        }
        archiveName = project.default_app_name + '.war'
    }

    // Include the swagger-ui so that /fits provides the fits API documentation
    from('../') {
        include 'swagger-ui/'
    }
}

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestRuntime.extendsFrom testRuntime
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-core:[2.17.1,3.0)'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:[2.17.1,3.0)'
    implementation 'org.opencadc:cadc-access-control:[1.1.23, 1.2.0)'
    implementation 'org.opencadc:cadc-data-ops-fits:[0.2.4,)'
    implementation 'org.opencadc:cadc-dali:[1.2.10,)'
    implementation 'org.opencadc:cadc-log:[1.1.2,2.0)'
    implementation 'org.opencadc:cadc-rest:[1.2.19,)'
    implementation 'org.opencadc:cadc-soda-server:[1.2.0,2.0.0)'
    implementation 'org.opencadc:cadc-vosi:[1.0,)'
    implementation 'org.opencadc:nom-tam-fits:1.16.4'

    testImplementation 'junit:junit:[4.12,5.0)'
    testImplementation 'org.mockito:mockito-core:[3.6.0,4)'
    testImplementation 'org.opencadc:cadc-test-uws:[1.2.2,2.0.0)'
    testImplementation 'org.opencadc:cadc-test-vosi:[1.0.9,2.0.0)'

    intTestImplementation 'org.opencadc:cadc-test-uws:[1.2.1,2.0.0)'
    intTestImplementation 'org.opencadc:cadc-test-vosi:[1.0.9,2.0.0)'
}

sourceSets {
    intTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/intTest/java')
        }
    }
}

tasks.withType(Test) {
    // Reset the report destinations so that intTests go to their own page
    reports.html.destination = file(reporting.baseDir.getAbsolutePath() + '/' + name)

    // Assign all Java system properties from the command line to the tests
    systemProperties System.properties as Map<String, ?>
}

task intTest(type: Test) {
    // set the configuration context
    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath

    // run the tests always
    outputs.upToDateWhen { false }
}
